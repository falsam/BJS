;Babylon.sbi - BeforeRender()

EnableExplicit

IncludeFile "babylon/babylon.sbi"

Enumeration
  #mf
  #mfRender
EndEnumeration

Global Scene, Camera, Light, Ground, GroundTex, GroundMat, Cylinder, Bloc, BlocMat, ShadowGenerator

Declare LoadGame()
Declare BlocAnim()
Declare RenderGame()

UseModule BJS

OpenWindow(#mf, 0, 0, 800, 600, "BeforeRender() process", #PB_Window_ScreenCentered)
CanvasGadget(#mfRender, 0, 0, 900, 600, #PB_Canvas_Transparent)

SetEnginePath("sbbjs")
InitEngine(@LoadGame(), #mfRender)

Procedure LoadGame()    
  Scene = CreateScene()
  If Scene
    
    ;Camera & Light
    Camera = CreateCamera("camera", 0, 6, 10, #BJS_ArcRotate)
    CameraLookAt(Camera, 0, 3, 0)
    
    CreateLight("light", 0, 5, 10, 1)
    Light = CreateLight("Point", 0, 10, -10, 0.8, #BJS_Point)
    
    ;Material
    GroundTex = LoadTexture("grass", "data/textures/grass1.jpg")
    GroundMat = CreateMaterial("grass")
    SetMaterialTexture(GroundMat, #BJS_Diffuse, GroundTex)
    ScaleMaterial(GroundMat, 5, 5)  
    
    BlocMat = CreateMaterial("bloc coloe")
    SetMaterialColor(BlocMat, #BJS_Diffuse, RGB(255, 0, 0))
    
    ;Mesh
    Ground = CreateGround("ground", 40, 40) 
    SetMeshMaterial(Ground, GroundMat)
    
    Cylinder = CreateCylinder("cylinder", 5, 0.5, 0.5)
    MoveMesh(Cylinder, 0, 2.5, -10)
    
    Bloc = CreateBox("bloc", 2, 2, 2)
    SetMeshMaterial(Bloc, BlocMat)
    MoveMesh(Bloc, 0, 4, -10)
    
    ;Shadow
    InitShadow(Light)
    ShadowEmitter(Bloc)
    RenderShadows(Ground)
    
    ;BeforeRender() function can be used to automate a task.
    BeforeRender(@BlocAnim())
    
    RenderLoop(@RenderGame())
  EndIf
EndProcedure

Procedure BlocAnim()
  Static y.f, i.f
  
  If MeshY(Bloc) <= 1 
    i = -0.5
  EndIf 
  If MeshY(Bloc) >= 4 
    i = 0.5
  EndIf 
  
  y - i/10
  
  MoveMesh(Bloc, 0, y, -10)
  RotateMesh(Bloc, 0, i, 0, #PB_Relative)
EndProcedure

Procedure RenderGame()
  RenderWorld() 
EndProcedure
; IDE Options = SpiderBasic 2.20 (Windows - x86)
; CursorPosition = 37
; FirstLine = 24
; Folding = -
; WebAppName = Before Render
; HtmlFilename = beforerender.html
; JavaScriptFilename = beforerender.js
; JavaScriptPath = sb
; iOSAppOrientation = 0
; AndroidAppOrientation = 0
; EnableXP
; CompileSourceDirectory