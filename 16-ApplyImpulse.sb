;Babylon.sbi - Apply Mesh Impulse
EnableExplicit

IncludeFile "babylon/babylon.sbi"
UseModule BJS

Enumeration
  #Font
  #mf
  #mfRender
EndEnumeration

Global Scene, Camera, Ground, GroundTex, GroundMat, Dim Boxs(9), BoxTex, BoxMat

Declare LoadGame()
Declare RenderGame()
Declare Resize()

LoadFont(#Font, "Arial", 22)
OpenWindow(#mf, 0, 0, 0, 0, "", #PB_Window_Background)
CanvasGadget(#mfRender, 0, 0, WindowWidth(#mf), WindowHeight(#mf), #PB_Canvas_Transparent)
BindEvent(#PB_Event_SizeWindow, @Resize())

InitEngine(@LoadGame(), #mfRender)
InitKey()

Procedure LoadGame()
  Protected n, Help
  
  Scene = CreateScene()
  If Scene
    
    ClearScene(RGB(178, 34, 34))
    Camera = CreateCamera("camera", 0, 10, 30, #BJS_ArcRotate)
    CreateLight("light", 0, 20, 0, 0.8)  
    
    ;Create static ground (Mass = 0) 
    Ground = CreateGround("ground", 100, 100)
    CreateMeshBody(Ground, #BJS_BoxBody, 0, 0.5, 0.5)
    GroundTex = LoadTexture("Data/textures/Beton1.png")
    GroundMat = CreateMaterial("ground")
    SetMaterialTexture(GroundMat, #BJS_Diffuse, GroundTex)
    ScaleMaterial(GroundMat, 5, 5)
    SetMeshMaterial(Ground, GroundMat)
    
    ;Create 10 Box (Physic)
    BoxTex = LoadTexture("data/textures/crate.png")
    BoxMat = CreateMaterial("crate")
    SetMaterialTexture(BoxMat, #BJS_Diffuse, BoxTex)
    
    For n = 0 To 9
      Boxs(n) = CreateBox("box", 3, 3, 3)
      MoveMesh(Boxs(n), (n * 4) - 18, 1.4, -30)
      SetMeshMaterial(Boxs(n), BoxMat)
      CreateMeshBody(Boxs(n), #BJS_BoxBody, 1, 1, 1)
    Next
    
    Help = TextGadget(-1, 20, WindowHeight(#mf) - 50, 400, 22, "Press the space key to send a pulse.")
    SetGadgetColor(Help, #PB_Gadget_FrontColor, RGB(255, 255, 255))
    SetGadgetFont(Help, FontID(#Font))
    RenderLoop(@RenderGame())    
  EndIf
EndProcedure

Procedure RenderGame()
  Protected n
   
  If KeyReleased(#PB_Key_Space)
    For n = 0 To 9
        ApplyMeshImpulse(Boxs(n), 0, Random(20, 10), Random(5, 2))
    Next
  EndIf
  RenderWorld() 
EndProcedure

Procedure Resize()
  ResizeGadget(#mfRender, #PB_Ignore, #PB_Ignore, WindowWidth(#mf), WindowHeight(#mf))
EndProcedure

; IDE Options = SpiderBasic 2.20 (Windows - x86)
; Folding = -
; WebAppName = Apply Impulse
; HtmlFilename = applyimpulse.html
; JavaScriptFilename = applyimpulse.js
; JavaScriptPath = sb
; iOSAppOrientation = 0
; AndroidAppOrientation = 0
; EnableXP
; CompileSourceDirectory